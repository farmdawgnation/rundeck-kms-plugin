plugins {
  id 'java-library'
  id "com.github.johnrengelman.shadow" version "6.1.0"
}

ext.pluginClassNames='me.frmr.rundeck.KmsConverterPlugin'
ext.pluginName = 'Rundeck KMS Plugin'
ext.pluginDescription = 'Encrypts and decrypts storace using Amazon KMS'


group = "me.frmr.rundeck"

targetCompatibility = '11.0'
sourceCompatibility = '11.0'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestCompile.extendsFrom compile
    integrationTestApi.extendsFrom api
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    implementation 'org.rundeck:rundeck-core:3.3.9-20210201'
    implementation 'org.rundeck:rundeck-storage-api:3.3.9-20210201'

    api 'com.amazonaws:aws-encryption-sdk-java:2.0.0'
    api 'com.amazonaws:aws-java-sdk:1.11.704'
}

task integrationTest(type: Test) {
    description = 'Run integration tests.'
    check.dependsOn(it)
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath + sourceSets.main.runtimeClasspath
    shouldRunAfter test
}

tasks.withType(Test) {
    useJUnitPlatform()
}

shadowJar {
    archiveName 'rundeck-kms-plugin-all.jar'
    zip64 true
    manifest {
        attributes(
            'Rundeck-Plugin-Version': '1.2',
            'Rundeck-Plugin-Archive': 'true',
            'Rundeck-Plugin-Classnames': pluginClassNames,
            'Rundeck-Plugin-Author': 'Matt Farmer',
            'Rundeck-Plugin-URL': 'https://github.com/farmdawgnation/rundeck-kms-plugin',
            'Rundeck-Plugin-Date': '2020-02-07',
            'Rundeck-Plugin-File-Version': '1.0',
            'Rundeck-Plugin-Name': pluginName,
            'Rundeck-Plugin-Description': pluginDescription
        )
    }
}

jar {
    from "$buildDir/output"
    archiveName 'rundeck-kms-plugin.jar'
    manifest {
        attributes(
            'Rundeck-Plugin-Version': '1.2',
            'Rundeck-Plugin-Archive': 'true',
            'Rundeck-Plugin-Classnames': pluginClassNames,
            'Rundeck-Plugin-Author': 'Matt Farmer',
            'Rundeck-Plugin-URL': 'https://github.com/farmdawgnation/rundeck-kms-plugin',
            'Rundeck-Plugin-Date': '2020-02-07',
            'Rundeck-Plugin-File-Version': '1.0',
            'Rundeck-Plugin-Name': pluginName,
            'Rundeck-Plugin-Description': pluginDescription
        )
    }
}

// task copyToLib(type: Copy) {
//     into "$buildDir/output/lib"
//     from configurations.pluginLibs
// }
